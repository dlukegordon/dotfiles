#!/usr/bin/env nu

# Tmux sessionizer, inspired by https://github.com/theprimeagen/.dotfiles/blob/master/bin/.local/scripts/tmux-sessionizer
# Converted to nushell, and with a few improvements of mine

def main [...args] {
    # Get the selected directory either from args or from fzf
    let selected = if ($args | length) > 0 {
        $args.0
    } else {
        let depth_0_dirs = [~/dotfiles ~/scratch ~/notes] | each {|d| $d | path expand} | where {|d| $d | path exists}
        let depth_1_dirs = [~/projects ~/gits ~/work]     | each {|d| $d | path expand} | where {|d| $d | path exists}

        let depth_1 = fd --exact-depth 1 --type d . ...$depth_1_dirs | lines

        # replace $home with ~ for brevity
        let dirs = ($depth_0_dirs | append $depth_1 | each { |it| $it | str replace -r $"^($nu.home-path)" "~" })
        ($dirs | to text | fzf | str trim)
    }

    if ($selected | is-empty) {
        exit 0
    }

    # Get the session name from the base directory name
    let selected_name = ($selected | path basename | str replace "." "_")

    let session_exists = (tmux has-session -t=($selected_name) | complete).exit_code == 0
    if not $session_exists {
        # Tmux doesn't have this session, so create
        # Replace ~ back to $home so tmux can understand the path
        let full_path = ($selected | str replace -r "^~" $nu.home-path)
        tmux new-session -ds $selected_name -c $full_path
    }

    if ($env | get -i TMUX) != null {
        # We're in tmux, so switch
        tmux switch-client -t $selected_name
    } else {
        # We're not in tmux, so attach
        tmux attach -t $selected_name
    }
}
